## slm-run  --config config.bok  -f queries/bok-graph.sparql --debug
PREFIX ns1: <http://example.org/course/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ex: <http://example.org/>
PREFIX align: <http://align.org/>
PREFIX prov: <http://provo.org/>

CONSTRUCT {
    ?s align:to [
        align:ku_source ?ku_source ;
        align:score ?score ;
        ns1:ka ?ka ;
        ns1:ku ?ku ;
        ns1:answer ?answer ;
        ns1:explain ?explain ;
        prov:wasGeneratedBy [
            prov:used ?model ;
      ]
    ]
} 
#SELECT DISTINCT ?s ?label ?ku_source ?score ?parcours ?ka ?ku ?answer ?explain 
WHERE {
    BIND(ex:SLM-FILE("/Users/molli-p/NUCS-KG/XP/licence2025/L3.ttl") AS ?input)
    BIND(ex:SLM-RDF(?input,"turtle") AS ?ginput)
    graph ?ginput { 
        { select * where {
            ?s ns1:content ?content .
            ?s rdfs:label ?label .
            ?s ns1:objective ?objective .
            ?s ns1:parcours ?parcours .
#            FILTER(CONTAINS(?parcours, "ALMA"))
        } # LIMIT  1 
       }
    }
    BIND(CONCAT("Label: ",STR(?label)," Objectif: ",STR(?objective),
                " Course content: ", STR(?content)," Course name: ",STR(?s)) AS ?UE)
    BIND(ex:SLM-SEARCH-FAISS(?UE,?s,3) AS ?retrieval_graph)
    GRAPH ?retrieval_graph {
        ?s ex:is_aligned_with ?bn .
        ?bn ex:has_score ?score .
        ?bn ex:has_source ?ku_source .
        ?bn ex:has_chunk ?chunk .
    }
#    OPTIONAL {
#        FILTER(xsd:float(?score) < 0.7)
        BIND (REPLACE(str(?ku_source), "file://.*/([^/]+)/[^/]+\\.txt$", "$1") AS ?ka) 
        BIND (REPLACE(str(?ku_source), "file://.*/([^/]+)/([^/]+)\\.txt$", "$2") AS ?ku)
        BIND(ex:SLM-READFILE(?ku_source) AS ?ku_content)

        BIND(CONCAT("""
        You are a JSON-LD API. Always respond only with a valid JSON-LD object, without explanation or formatting.

        The following describes the content of a lecture and a Knowledge Unit (KU) in Computer Science.

        <page1>""",STR(?UE),"""</page1>
        <page2>""",STR(?ku_content),"""</page2>

        Determine whether the lecture (page1) substantially covers the knowledge described in the KU (page2).

        Respond only using JSON-LD, with the following structure:
        {
        "@context": "http://schema.org/",
        "@type": "Report",
        "http://schema.org/answer": "A",
        "http://schema.org/explain": "B"
        }

        Replace A with "1" if the lecture covers a large subset of the KU, or "0" if it does not.
        Replace B with a **very short explanation** of your decision.
        """) AS ?prompt)
        VALUES ?model {
            "llama3-8b-8192"
            "qwen-qwq-32b" 
            "deepseek-r1-distill-llama-70b"
        }
        BIND(ex:SLM-LLMGRAPH_GROQM(?prompt,?s,?model) AS ?llm_graph)

        GRAPH ?llm_graph {
            ?s ex:has_schema_type ?root .
            ?root a <http://schema.org/Report>  .
            ?root <http://schema.org/answer> ?answer .
            ?root <http://schema.org/explain> ?explain .    
        }
#    }
} 