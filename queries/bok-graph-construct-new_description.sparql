PREFIX schema: <http://schema.org/>
PREFIX ex: <http://example.org/>
PREFIX align: <http://align.org/>
PREFIX prov: <http://provo.org/>

CONSTRUCT {
  ?s align:to [
    align:ku_source ?ku_source ;
    align:score ?score ;
    schema:ka ?ka ;
    schema:ku ?ku ;
    schema:answer ?answer ;
    schema:explain ?explain ;
    prov:wasGeneratedBy [
      prov:used ?model
    ]
  ] .
}
WHERE {
  {
    SELECT ?s (GROUP_CONCAT(DISTINCT ?name) AS ?labels)
               (GROUP_CONCAT(DISTINCT ?desc) AS ?descriptions)
    WHERE {
      ?s a schema:Course .
      OPTIONAL { ?s schema:name ?name }
      OPTIONAL { ?s schema:description ?desc }
    }
    GROUP BY ?s
  }

  BIND(CONCAT(
    "Course name: ", STR(?labels),
    "\nDescription: ", STR(?descriptions)
  ) AS ?UE)

  BIND(ex:SLM-SEARCH-FAISS(?UE, ?s, 3) AS ?retrieval_graph)
  GRAPH ?retrieval_graph {
    ?s ex:is_aligned_with ?bn .
    ?bn ex:has_score ?score .
    ?bn ex:has_source ?ku_source .
  }

  BIND(REPLACE(STR(?ku_source), "file://.*/([^/]+)/[^/]+\\.txt$", "$1") AS ?ka)
  BIND(REPLACE(STR(?ku_source), "file://.*/([^/]+)/([^/]+)\\.txt$", "$2") AS ?ku)
  BIND(ex:SLM-READFILE(?ku_source) AS ?ku_content)

  BIND(CONCAT(
    "You are a JSON-LD API. Always respond only with a valid JSON-LD object.\n\n",
    "<page1>", STR(?UE), "</page1>\n",
    "<page2>", STR(?ku_content), "</page2>\n\n",
    "Determine whether the lecture (page1) covers the KU (page2).\n",
    "Respond only with:\n",
    "{ \"@context\": \"http://schema.org/\", \"@type\": \"Report\", \"http://schema.org/answer\": \"A\", \"http://schema.org/explain\": \"B\" }"
  ) AS ?prompt)

  VALUES ?model { "llama3-8b-8192" }

  BIND(ex:SLM-LLMGRAPH_GROQM(?prompt, ?s, ?model) AS ?llm_graph)

  GRAPH ?llm_graph {
    ?s ex:has_schema_type ?root .
    ?root a schema:Report ;
          schema:answer ?answer ;
          schema:explain ?explain .
  }
}
