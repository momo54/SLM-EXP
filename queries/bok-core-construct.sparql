# run with slm-run --config config.bok -f queries/bok-core-construct.sparql --debug
# Carefull, if nb_hours is not found -> the join will remove the result... -> optional??

PREFIX bok: <http://example.org/bok/>
PREFIX ex: <http://example.org/>

CONSTRUCT {
    ?ku bok:path ?p2 .
    ?ku bok:part_of ?ka .
    ?ku bok:content ?ku_content .
    ?ku bok:cs_core_hours ?cs_core .
    ?ku bok:ka_core_hours ?ka_core .
} 
#SELECT ?p ?ka ?ku ?cs_core ?ka_core 
WHERE {
    ## init recursion
    BIND(ex:SLM-FILE("./data/BodyOfKnowledge") AS ?root)
    BIND(ex:SLM-READDIR(?root,?root) AS ?ginit)
    BIND("""
    PREFIX ex: <http://example.org/>
    CONSTRUCT {
        ?p ex:has_path ?p1 .
        ?p1 ex:has_type ?t1.
        ?p1 ex:has_size ?s1 .
        ?new ex:has_path ?p2 .
        ?p2 ex:has_type ?t2.
        ?p2 ex:has_size ?s2 .

    } WHERE {
        graph ?gin {
          {
            ?p ex:has_path ?p1 .
            ?p1 ex:has_type ?t1 .
            ?p1 ex:has_size ?s1 .
            FILTER (str(?t1)="file")
          } UNION {
            ?p ex:has_path ?p1 .
            ?p1 ex:has_type ?t1 .
            ?p1 ex:has_size ?s1 .
            FILTER (str(?t1)="directory")
            BIND(ex:SLM-READDIR(?p1,?p1) as ?rdir)
            OPTIONAL {
                GRAPH ?rdir {
                    ?new ex:has_path ?p2 .
                    ?p2 ex:has_type ?t2.
                    ?p2 ex:has_size ?s2 .
                }
            }}
        }
    } """ AS ?query_str)
    BIND(ex:SLM-RECURSE(?query_str,?ginit,3) AS ?out)
    GRAPH ?out {
        ?p ex:has_path ?p2 .
        ?p2 ex:has_type "file" .
#        FILTER (str(?t2)="file")
        BIND (REPLACE(str(?p2), "file://.*/([^/]+)/[^/]+\\.txt$", "$1") AS ?ka) 
        BIND (REPLACE(str(?p2), "file://.*/([^/]+)/([^/]+)\\.txt$", "$2") AS ?ku)
         BIND(ex:SLM-READFILE(?p2) AS ?ku_content)
         ###
         ### Extracting hours is not reliable... ...
         ###
        BIND(CONCAT("""
        You are a JSON-LD API. Always respond only with a valid JSON-LD object, without explanation or formatting.

        The following describes the content of a  Knowledge Unit (KU) in Computer Science.

        <page>""",STR(?ku_content),"""</page>

        It contains a fragment as:
        HOURS
        	CS Core = 11
        	KA Core = 6

        Respond only using JSON-LD, with the following structure:
        {
        "@context": "http://schema.org/",
        "@type": "Report",
        "http://schema.org/CS-Core": "A",
        "http://schema.org/KA-Core": "B"
        }

        Replace A with the number attached to CS Core.
        Replace B with the number attached to KA-Core.
        """) AS ?prompt)
         VALUES ?model {
            "llama3-8b-8192"
#            "qwen-qwq-32b" 
#            "deepseek-r1-distill-llama-70b"
        }
        BIND(ex:SLM-LLMGRAPH_GROQM(?prompt,?p2,?model) AS ?llm_graph)
        GRAPH ?llm_graph {
            ?p2 ex:has_schema_type ?p2bn  .
            ?p2bn a <http://schema.org/Report>  .
            ?p2bn <http://schema.org/CS-Core> ?cs_core  .
            ?p2bn <http://schema.org/KA-Core> ?ka_core  .    
        }
     } # graph_out
} # limit 5
        
